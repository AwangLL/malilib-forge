
buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

minecraft {
    mappings channel: config.mcp_mappings_channel, version: config.mcp_mappings_version

    //makeObfSourceJar = false

    ext.mod_version = config.mod_version

    if (config.mod_version.endsWith('-dev')) {
        config.mod_version = config.mod_version + "." + new Date().format('yyyyMMdd.HHmmss')
    }

    //replaceIn "MaLiLibReference.java"
    //replace "@MOD_VERSION@", mod_version
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + config.minecraft_version + '-' + config.forge_version
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

/*
sourceSets {
    main {
        ext.refMap = 'mixins.' + config.mod_id + '.refmap.json'
    }
}
*/

group = config.group + "." + config.mod_id // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_file_name + '-' + config.minecraft_version_out
version = project.config.mod_version

/**
 * This section allows you to customise your generated jar (litemod) file. By
 * default it includes the generated litemod.json file, however if you prefer to
 * include your own file from resources, simply remove the line below.
 */
jar {
    // Remove the "-mc1.12" suffix from the file name
    //classifier = ""
    // Don't append a 'mod-' filename prefix >_>
    baseName = archivesBaseName

    manifest {
        attributes([
            "Specification-Title": "${config.mod_id}",
            "Specification-Vendor": "${config.author}",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": "${config.mod_name}",
            "Implementation-Version": "${config.mod_version}",
            "Implementation-Vendor": "${config.author}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "mixins.malilib.json"
        ])
    }
}

mixin {
    add sourceSets.main, 'mixins.' + config.mod_id + '.refmap.json'
}

processResources
{
    // Exclude the GIMP image files
    exclude '**/*.xcf'
    exclude '**/xcf'

    // this will ensure that this task is redone when the versions change.
    inputs.property "mod_version", project.config.mod_version
    inputs.property "minecraft_version", project.config.minecraft_version
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    // This classifier is standard and should not be changed
    classifier = 'deobf'
}

// This section enables the last two tasks
artifacts {
    //archives sourceJar
    archives deobfJar
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            artifact deobfJar
            //artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../../CommonMaven"
        }
    }
}
